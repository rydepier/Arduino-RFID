/*
 * The library file MFRC522.h has a wealth of useful info. Please read it.
 * 
 * This sketch when first run will store the UID of the first card scanned
 * if Digital Pin 4 is connected to Gnd.
 * When this card is scanned again the Green LED will light
 * All other cards will turn both LEDs off.
 * After 1 second the Green LED will be off and the Red LED will be on.
 * 
 * Stored UIDs are lost if the power is removed or the Sketch is reset
 * 
 * Connect a flying lead to pin 4, if the learn Mode is required then
 * connect this to Gnd.
 * 
 * UIDs are printed out in HEX and Decimal in the Serial Monitor
 * 
 * Pin layout should be as follows:
 * (Bi Directional Logic Level Convertors are required as the device is 3.3volts
 * and two are required for the 5 signal lines)
 * Signal     Pin              Pin
 *            Arduino Uno      MFRC522 board
 * -----------------------------------------
 * Reset      5                RST
 * SPI SS     10               SDA
 * SPI MOSI   11               MOSI
 * SPI MISO   12               MISO
 * SPI SCK    13               SCK
 *
 *Connect Digital Pin 4 to Gnd to activate the Learn Mode
 */

#include <SPI.h>
#include <MFRC522.h>

#define SS_PIN 10
#define RST_PIN 5
#define redLed 2
#define greenLed 3
MFRC522 mfrc522(SS_PIN, RST_PIN);	// Create MFRC522 instance.
String UIDstring;
boolean learnMode = false; // dont ask to remember this card number
String knownCard = "";

void setup() {
	Serial.begin(9600);	// Initialize serial communications with the PC
	SPI.begin();			// Init SPI bus
	mfrc522.PCD_Init();	// Init MFRC522 card
  pinMode(4, INPUT_PULLUP);
  pinMode(redLed, OUTPUT);
  pinMode(greenLed, OUTPUT);
  digitalWrite(redLed, HIGH);
  digitalWrite(greenLed, LOW);
  if(digitalRead(4) == 0){
    learnMode = true;
    Serial.println("Learn Mode ON");
  }
  else{
    learnMode = false;
    Serial.println("Learn Mode OFF");
  }
}

void loop() {
	// Look for new cards
	if ( ! mfrc522.PICC_IsNewCardPresent()) {
		return;
	}

	// Select one of the cards
	if ( ! mfrc522.PICC_ReadCardSerial()) {
		return;
	}

	// Dump debug info about the card. PICC_HaltA() is automatically called.
	//mfrc522.PICC_DumpToSerial(&(mfrc522.uid));

  // Dump UID
  Serial.print("Card UID:");
  UIDstring = "";
  for (byte i = 0; i < mfrc522.uid.size; i++) {
    UIDstring = UIDstring + String(mfrc522.uid.uidByte[i]);
    Serial.print(mfrc522.uid.uidByte[i] < 0x10 ? " 0" : " ");
    Serial.print(mfrc522.uid.uidByte[i], HEX);
  } 
  Serial.println();
  if(learnMode){
    knownCard = UIDstring;
    learnMode = false; // turn it off
  }
  Serial.println(UIDstring);
  // light correct LED
  if (UIDstring == knownCard){
    digitalWrite(redLed, LOW);
    digitalWrite(greenLed, HIGH);
  }
  else{
    digitalWrite(redLed, LOW); // turn both LEDS off, its the wrong card
    digitalWrite(greenLed, LOW);
  }
  delay(1000);
  digitalWrite(redLed, HIGH);
  digitalWrite(greenLed, LOW);
}
